buildscript {
    ext.junit_milestone = ''
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'org.jetbrains.intellij'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group 'uk.co.reecedunn.intellij.plugin.marklogic'
    version '0.1'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

dependencies {
    compile 'com.marklogic:marklogic-xcc:9.0.2'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0$junit_milestone"
    // See: http://junit.org/junit5/docs/snapshot/user-guide/#running-tests-ide-intellij-idea
    testRuntime("org.junit.platform:junit-platform-launcher:1.0.0$junit_milestone")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0$junit_milestone")
    testRuntime("org.junit.vintage:junit-vintage-engine:4.12.0$junit_milestone")
}

intellij {
    def ideaVersion = System.getenv("IDEA_VERSION")
    version = ideaVersion ? ideaVersion : 'IC-2017.2' // IntelliJ IDEA dependency
    println "Building for IntelliJ version: ${version}"

    plugins 'coverage'  // Bundled plugin dependencies
    pluginName 'marklogic-intellij-plugin'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
